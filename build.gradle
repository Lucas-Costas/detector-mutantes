plugins {
    id 'groovy'
    id 'java'
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id "io.freefair.lombok" version "5.3.0"
    id 'jacoco'
}

group 'ar.com.magneto'
version '1.6'

sourceCompatibility = 11

sourceSets {
    acceptanceTest {
        groovy {
            compileClasspath += main.output
            compileClasspath += main.compileClasspath
            compileClasspath += test.output
            compileClasspath += test.compileClasspath
            runtimeClasspath += test.runtimeClasspath
            srcDir file('src/acceptance-test/groovy')
        }
        resources.srcDir file('src/acceptance-test/resources')
    }
}

repositories {
    mavenCentral()
}

task verify(type: Test) {
    testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
    classpath = sourceSets.acceptanceTest.runtimeClasspath

    dependsOn 'test'
    verify.mustRunAfter 'test'
}

dependencies {
    compile 'redis.clients:jedis:3.3.0'
    compile 'org.neo4j.driver:neo4j-java-driver:4.1.1'
    compile 'org.springframework.boot:spring-boot-starter-web'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.spockframework:spock-spring:1.3-groovy-2.4'
}

jacocoTestReport {
    executionData.setFrom  files("${buildDir}/jacoco/verify.exec", "${buildDir}/jacoco/test.exec")
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ["**/MutantDetectionAplication**"])
        })
    }
    reports {
        html.destination file("${buildDir}/reports/jacocoHtml")
    }
    dependsOn(build,verify)
}